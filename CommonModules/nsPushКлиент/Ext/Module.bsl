Процедура ИнициализацияДанныхPush() Экспорт
	#Если МобильноеПриложениеКлиент Тогда
	Попытка
		
		ИдентификаторОтправителя = nsОбщегоНазначенияВызовСервера.ПолучитьFCMИдентификаторОтправителя();
		МобильнаяОС = nsОбщегоНазначенияВызовСервера.ПолучитьМобильнуюОС();
		
		Если МобильнаяОС = ПредопределенноеЗначение("Перечисление.nsМобильныеОС.Android") Тогда
			ИдентификаторПодписчика = ДоставляемыеУведомления.ПолучитьИдентификаторПодписчикаУведомлений(ИдентификаторОтправителя);	
		ИначеЕсли МобильнаяОС = ПредопределенноеЗначение("Перечисление.nsМобильныеОС.iOS") Тогда
			ИдентификаторПодписчика = ДоставляемыеУведомления.ПолучитьИдентификаторПодписчикаУведомлений();	
		Иначе
			Возврат;
		КонецЕсли;
		
		nsPushВызовСервера.УстановитьИдентификаторПодписчика(ИдентификаторПодписчика);
	Исключение
		nsОбработкаОшибок.ЗаписатьОшибку(ОписаниеОшибки());
	КонецПопытки; 
	#КонецЕсли
КонецПроцедуры

Процедура ПодключитьОбработчикPush() Экспорт
	#Если МобильноеПриложениеКлиент Тогда
	ОбработчикУведомленийОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикУведомлений", nsPushКлиент);
	ДоставляемыеУведомления.ПодключитьОбработчикУведомлений(ОбработчикУведомленийОписаниеОповещения);
	#КонецЕсли
КонецПроцедуры

Процедура ОбработчикУведомлений(Уведомление, Локальное, Показано, ДополнительныеПараметры) Экспорт
	СообщениеPush = "Данные: " + Уведомление.Данные + Символы.ПС + "Текст: " + Уведомление.Текст;
	ПоказатьПредупреждение(, СообщениеPush,, "PUSH");		
КонецПроцедуры
